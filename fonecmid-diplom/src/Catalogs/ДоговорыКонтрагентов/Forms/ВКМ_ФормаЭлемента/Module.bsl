#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	// Добавление ВКМ_ГруппаСрокДействияДоговора
	ВКМ_ГруппаСрокДействияДоговора = Элементы.Вставить("ВКМ_ГруппаСрокДействияДоговора", Тип("ГруппаФормы"),
		ЭтотОбъект, );
	ВКМ_ГруппаСрокДействияДоговора.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		
	// Добавление поля ВКМ_ДатаНачала		
	ВКМ_ДатаНачала = Элементы.Вставить("ВКМ_ДатаНачала", Тип("ПолеФормы"), Элементы.ВКМ_ГруппаСрокДействияДоговора, );
	ВКМ_ДатаНачала.Вид = ВидПоляФормы.ПолеВвода;
	ВКМ_ДатаНачала.ПутьКДанным = "Объект.ВКМ_ДатаНачала";
	ВКМ_ДатаНачала.Заголовок = "Период действия";			
	
	// Добавление декорации ВКМ_ДекорацияНадписьПо
	ДекорацияНадписьПо = Элементы.Вставить("ВКМ_ДекорацияНадписьПо", Тип("ДекорацияФормы"),
		Элементы.ВКМ_ГруппаСрокДействияДоговора, );
	ДекорацияНадписьПо.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияНадписьПо.Заголовок = "-";
		
	// Добавление поля ВКМ_ДатаОкончания		
	ВКМ_ДатаОкончания = Элементы.Вставить("ВКМ_ДатаОкончания", Тип("ПолеФормы"),
		Элементы.ВКМ_ГруппаСрокДействияДоговора, );
	ВКМ_ДатаОкончания.Вид = ВидПоляФормы.ПолеВвода;
	ВКМ_ДатаОкончания.ПутьКДанным = "Объект.ВКМ_ДатаОкончания";
	ВКМ_ДатаОкончания.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;			

	// Добавление поля ВКМ_СуммаАбонентскойПлаты	
	ВКМ_СуммаАбонентскойПлаты = Элементы.Вставить("ВКМ_СуммаАбонентскойПлаты", Тип("ПолеФормы"), , );
	ВКМ_СуммаАбонентскойПлаты.Вид = ВидПоляФормы.ПолеВвода;
	ВКМ_СуммаАбонентскойПлаты.ПутьКДанным = "Объект.ВКМ_СуммаАбонентскойПлаты";
	 
	// Добавление поля ВКМ_СтоимостьЧаса	
	ВКМ_СтоимостьЧаса = Элементы.Вставить("ВКМ_СтоимостьЧаса", Тип("ПолеФормы"), , );
	ВКМ_СтоимостьЧаса.Вид = ВидПоляФормы.ПолеВвода;
	ВКМ_СтоимостьЧаса.ПутьКДанным = "Объект.ВКМ_СтоимостьЧаса";

	УстановитьВидимостьЭлементовПоВидуДоговора();

КонецПроцедуры
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидДоговораПриИзменении(Элемент)

	УстановитьВидимостьЭлементовПоВидуДоговора();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьВидимостьЭлементовПоВидуДоговора()
	
	ЭтоАбонентскийДоговор = Объект.ВидДоговора = ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание");
	
	Элементы.ВКМ_ГруппаСрокДействияДоговора.Видимость = ЭтоАбонентскийДоговор;
	Элементы.ВКМ_СуммаАбонентскойПлаты.Видимость = ЭтоАбонентскийДоговор;
	Элементы.ВКМ_СтоимостьЧаса.Видимость = ЭтоАбонентскийДоговор;
	
КонецПроцедуры

#КонецОбласти
