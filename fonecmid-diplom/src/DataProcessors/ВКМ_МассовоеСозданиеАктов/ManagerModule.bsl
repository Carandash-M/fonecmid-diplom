#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Формирует список договоров и связанных с ними реализаций за указанный месяц. При необходимости создает новые реализации.
//
// Параметры:
//   Дата - Дата - Дата, для которой формируется список
//   СоздатьОбъект - Булево - Признак необходимости создания реализации, если она отсутствует
//
// Возвращаемое значение:
//   Массив из Структура - Массив структур с полями "Договор" и "Реализация"
Функция СозданиеСпискаНаСервере(Дата, СоздатьОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.Ссылка КАК Ссылка,
	|	ДоговорыКонтрагентов.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ДанныеДоговоров
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	&Дата МЕЖДУ ДоговорыКонтрагентов.ВКМ_ДатаНачала И ДоговорыКонтрагентов.ВКМ_ДатаОкончания
	|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Договор КАК Договор
	|ПОМЕСТИТЬ ВТ_ДанныеРеализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачало И &ДатаОкончание
	|	И РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДоговоров.Ссылка КАК Договор,
	|	ВТ_ДанныеРеализации.Ссылка КАК Реализация,
	|	ВТ_ДанныеДоговоров.Организация КАК Организация,
	|	ВТ_ДанныеДоговоров.Контрагент КАК Контрагент
	|ИЗ
	|	ВТ_ДанныеДоговоров КАК ВТ_ДанныеДоговоров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеРеализации КАК ВТ_ДанныеРеализации
	|		ПО ВТ_ДанныеДоговоров.Ссылка = ВТ_ДанныеРеализации.Договор";

	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ДатаНачало", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаОкончание", КонецМесяца(Дата));

	РезультатЗапроса = Запрос.Выполнить().Выгрузить();

	МассивРеализаций = Новый Массив;

	Для Каждого СтрокаРезультата Из РезультатЗапроса Цикл
		ТекущаяРеализация = СтрокаРезультата.Реализация;

		Если СоздатьОбъект И Не ЗначениеЗаполнено(ТекущаяРеализация) Тогда
			ТекущаяРеализация = СозданиеРеализации(СтрокаРезультата, КонецМесяца(Дата));
		КонецЕсли;

		МассивРеализаций.Добавить(Новый Структура("Договор, Реализация", СтрокаРезультата.Договор, ТекущаяРеализация));

	КонецЦикла;

	Возврат МассивРеализаций;

КонецФункции

// Функция создает, заполняет и проводит документ РеализацииТоваровУслуг
// на основе данных из структуры "Результат".
//
// Параметры:
//  Результат - Структура - Данные для заполнения документа (Контрагент, Организация, Договор).
//  Дата - Дата - Дата документа.
//
// Возвращаемое значение:
//  ДокументСсылка.РеализацияТоваровУслуг - Ссылка на созданный и проведенный документ.
//  Неопределено - если возникла ошибка или документ не прошел проверку заполнения.
Функция СозданиеРеализации(Результат, Дата)

	НовыйДок = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
	НовыйДок.Дата = Дата;
	НовыйДок.Договор = Результат.Договор;
	НовыйДок.Контрагент = Результат.Контрагент;
	НовыйДок.Организация = Результат.Организация;
	НовыйДок.ВКМ_ВыполнитьАвтозаполнение();
	НовыйДок.Ответственный = Пользователи.ТекущийПользователь();
	
	// Проверка на корректность заполнения перед транзакцией
	Если Не НовыйДок.ПроверитьЗаполнение() Тогда
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
			"Не удалось создать документ реализации по Договору: %1. Не все обязательные данные заполнены",
			Результат.Договор));
		Возврат Неопределено;
	КонецЕсли;

	Попытка
		НачатьТранзакцию();

		НовыйДок.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);

		ЗафиксироватьТранзакцию();

		Возврат НовыйДок.Ссылка;

	Исключение
		// В случае любой ошибки откатываем все изменения
		ОтменитьТранзакцию();

		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ТекстОшибки = ОбработкаОшибок.ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);

		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
			"Не удалось провести документ реализации по Договору: %1. Ошибка: %2", Результат.Договор, ТекстОшибки));

		ЗаписьЖурналаРегистрации("ВКМ_МассовоеСозданиеАктов.СозданиеРеализации", УровеньЖурналаРегистрации.Ошибка, , ,
			"Ошибка при создании и проведении документа: " + ТекстОшибки);

		Возврат Неопределено;
	КонецПопытки;

КонецФункции

#КонецОбласти

#КонецЕсли