#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_ВКМ_ОбслуживаниеКлиента" Тогда

		ОбновитьПланировщик();

	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УстановитьПериодПланировщика(ТекущаяДатаСеанса());
	
	ЗаполнитьСпециалистовВПланировщике();

	ОбновитьПланировщик();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПланировщикПриСменеТекущегоПериодаОтображения(Элемент, ТекущиеПериодыОтображения, СтандартнаяОбработка)

	СтандартнаяОбработка	= Ложь;

	Если ТекущиеПериодыОтображения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	НовыйПериод = ТекущиеПериодыОтображения[0];

	УстановитьПериодПланировщика(НовыйПериод.Конец);
	
	ОбновитьПланировщик();

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикВыбор(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка	= Ложь;

	ПараметрыФормы = Новый Структура("Ключ", Элемент.ВыделенныеЭлементы[0].Значение);

	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиента.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередНачаломБыстрогоРедактирования(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка	= Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПланировщикПередСозданием(Элемент, Начало, Конец, ЗначенияИзмерений, Текст, СтандартнаяОбработка)

	СтандартнаяОбработка	= Ложь;

	ЗначенияЗаполнения	= Новый Структура;
	ЗначенияЗаполнения.Вставить("ВКМ_Специалист", ЗначенияИзмерений.Получить("Специалист"));
	ЗначенияЗаполнения.Вставить("ВКМ_ДатаПроведенияРабот", НачалоДня(Начало));
	ЗначенияЗаполнения.Вставить("ВКМ_ВремяНачалаРабот", Начало);
	ЗначенияЗаполнения.Вставить("ВКМ_ВремяОкончанияРабот", Конец);

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);

	ОткрытьФорму("Документ.ВКМ_ОбслуживаниеКлиента.ФормаОбъекта", ПараметрыФормы);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)

	ОбновитьПланировщик();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьПланировщик()

	НачалоПериода = Планировщик.ТекущиеПериодыОтображения[0].Начало;
	КонецПериода  = Планировщик.ТекущиеПериодыОтображения[0].Конец;

	Планировщик.Элементы.Очистить();

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Обслуживание.Ссылка КАК Ссылка,
	|	Обслуживание.ВКМ_Клиент.Наименование КАК КлиентНаименование,
	|	Обслуживание.ВКМ_ОписаниеПроблемы КАК Описание,
	|	Обслуживание.ВКМ_Специалист КАК Специалист,
	|	Обслуживание.ВКМ_ДатаПроведенияРабот КАК ДатаРабот,
	|	Обслуживание.ВКМ_ВремяНачалаРабот КАК ВремяНачала,
	|	Обслуживание.ВКМ_ВремяОкончанияРабот КАК ВремяОкончания
	|ИЗ
	|	Документ.ВКМ_ОбслуживаниеКлиента КАК Обслуживание
	|ГДЕ
	|	Обслуживание.ПометкаУдаления = ЛОЖЬ
	|	И Обслуживание.ВКМ_ДатаПроведенияРабот МЕЖДУ &НачалоПериода И &КонецПериода";

	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);

	ВыборкаЗаявки = Запрос.Выполнить().Выбрать();

	Пока ВыборкаЗаявки.Следующий() Цикл
		
		// Собираем дату и время
		СекундСНачалаСутокНачало = (Час(ВыборкаЗаявки.ВремяНачала) * 3600) + (Минута(ВыборкаЗаявки.ВремяНачала) * 60)
			+ Секунда(ВыборкаЗаявки.ВремяНачала);
		СекундСНачалаСутокКонец  = (Час(ВыборкаЗаявки.ВремяОкончания) * 3600) + (Минута(ВыборкаЗаявки.ВремяОкончания)
			* 60) + Секунда(ВыборкаЗаявки.ВремяОкончания);

		НачалоРабот = НачалоДня(ВыборкаЗаявки.ДатаРабот) + СекундСНачалаСутокНачало;
		ОкончаниеРабот = НачалоДня(ВыборкаЗаявки.ДатаРабот) + СекундСНачалаСутокКонец;

		ЭлементПланировщика = Планировщик.Элементы.Добавить(НачалоРабот, ОкончаниеРабот);
		ЭлементПланировщика.Текст = "" + ВыборкаЗаявки.КлиентНаименование + Символы.ПС + ВыборкаЗаявки.Описание;
		ЭлементПланировщика.Значение = ВыборкаЗаявки.Ссылка;

		Если НачалоРабот >= ТекущаяДатаСеанса() Тогда
			ЭлементПланировщика.ЦветФона  = WebЦвета.СветлоЗолотистый;
			ЭлементПланировщика.ЦветРамки = WebЦвета.Золотистый;
		Иначе
			ЭлементПланировщика.ЦветФона  = WebЦвета.НейтральноЗеленый;
			ЭлементПланировщика.ЦветРамки = WebЦвета.ЦветМорскойВолныНейтральный;
		КонецЕсли;
		
		// Привязка к специалисту
		СоответствиеИзмерений = Новый Соответствие;
		СоответствиеИзмерений.Вставить("Специалист", ВыборкаЗаявки.Специалист);

		ЭлементПланировщика.ЗначенияИзмерений = Новый ФиксированноеСоответствие(СоответствиеИзмерений);

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСпециалистовВПланировщике()	
	
	Планировщик.Измерения.Очистить();
	ИзмерениеСпециалисты = Планировщик.Измерения.Добавить("Специалист");
	
ПрофильСпециалиста = "Специалист";

Запрос = Новый Запрос;
Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ГруппыДоступаПользователи.Пользователь КАК Ссылка,
	|	ГруппыДоступаПользователи.Пользователь.Наименование КАК Наименование
	|ИЗ
	|	Справочник.ГруппыДоступа.Пользователи КАК ГруппыДоступаПользователи
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ГруппыДоступа КАК ГруппыДоступа
	|		ПО ГруппыДоступаПользователи.Ссылка = ГруппыДоступа.Ссылка
	|ГДЕ
	|	ГруппыДоступа.Профиль.Наименование = &НаименованиеПрофиля
	|	И НЕ ГруппыДоступаПользователи.Пользователь.Недействителен
	|	И НЕ ГруппыДоступа.ПометкаУдаления
	|	И НЕ ГруппыДоступаПользователи.Пользователь.ПометкаУдаления";

Запрос.УстановитьПараметр("НаименованиеПрофиля", ПрофильСпециалиста);

	
	ВыборкаСпециалисты = Запрос.Выполнить().Выбрать();
	
	Пока ВыборкаСпециалисты.Следующий() Цикл
		ЭлементИзмерения = ИзмерениеСпециалисты.Элементы.Добавить(ВыборкаСпециалисты.Ссылка);
		ЭлементИзмерения.Текст = ВыборкаСпециалисты.Наименование;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьПериодПланировщика(ЦентральнаяДата)
	
	НачалоПериода = НачалоНедели(ЦентральнаяДата);
	КонецПериода  = КонецНедели(ЦентральнаяДата);
	
	Планировщик.ТекущиеПериодыОтображения.Очистить();
	Планировщик.ТекущиеПериодыОтображения.Добавить(НачалоПериода, КонецПериода);
	
	Заголовок = "Календарь обслуживания клиентов на период: " + ПредставлениеПериода(НачалоПериода, КонецПериода);
	
КонецПроцедуры

#КонецОбласти
