#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ВКМ_Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ВКМ_СформироватьДвиженияПоРегистру(
		Движения.ВКМ_ДополнительныеНачисления, ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ВКМ_ФиксированнаяПремия,
		Новый Структура("ВКМ_СуммаПремии", 0));

	ВКМ_СформироватьДвиженияПоРегистру(
		Движения.ВКМ_Удержания, ПланыВидовРасчета.ВКМ_Удержания.ВКМ_НДФЛ, Новый Структура("Процент", 13));
		
	ВКМ_ДвиженияВзаиморасчетыССотрудниками();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует движения по регистру расчета для фиксированной премии.
//
// Параметры:
//   Регистр - РегистрРасчетаМенеджер - Менеджер записей регистра расчета, в который создаются движения
//   ВидРасчета - ПланВидовРасчетаСсылка - Вид расчета для формируемых движений
//   ПараметрыРасчета - Структура - Параметры расчета, содержащие сумму премии или процент
Процедура ВКМ_СформироватьДвиженияПоРегистру(Регистр, ВидРасчета, ПараметрыРасчета)
	
	Регистр.Записывать = Истина;

	Для Каждого Строка Из ВКМ_СписокСотрудников Цикл

		Если Строка.ВКМ_Сотрудник = Справочники.Пользователи.ПустаяСсылка() Тогда
			Продолжить; // Пропускаем строки с незаполненным сотрудником
		КонецЕсли;
		
		Движение = Регистр.Добавить();
		Движение.ПериодРегистрации = Дата;
		Движение.ВидРасчета = ВидРасчета;
		Движение.ВКМ_Сотрудник = Строка.ВКМ_Сотрудник;
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		
		Если ПараметрыРасчета.Свойство("ВКМ_СуммаПремии") Тогда
			Движение.ВКМ_Результат = Строка.ВКМ_СуммаПремии;
		ИначеЕсли ПараметрыРасчета.Свойство("Процент") Тогда
			Движение.ВКМ_Результат = Строка.ВКМ_СуммаПремии * ПараметрыРасчета.Процент / 100;
		КонецЕсли;

	КонецЦикла;

	Регистр.Записать();
	
КонецПроцедуры

Процедура ВКМ_ДвиженияВзаиморасчетыССотрудниками() 
	
	// регистр ВзаиморасчетыССотрудниками
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;

	Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ВКМ_НачислениеФиксированнойПремииВКМ_СписокСотрудников.ВКМ_Сотрудник КАК Сотрудник,
		|	СУММА(ВКМ_НачислениеФиксированнойПремииВКМ_СписокСотрудников.ВКМ_СуммаПремии) КАК СуммаПремии
		|ИЗ
		|	Документ.ВКМ_НачислениеФиксированнойПремии.ВКМ_СписокСотрудников КАК
		|		ВКМ_НачислениеФиксированнойПремииВКМ_СписокСотрудников
		|ГДЕ
		|	ВКМ_НачислениеФиксированнойПремииВКМ_СписокСотрудников.Ссылка = &Ссылка
		|СГРУППИРОВАТЬ ПО
		|	ВКМ_НачислениеФиксированнойПремииВКМ_СписокСотрудников.ВКМ_Сотрудник";

	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.Выполнить().Выбрать();

	Пока РезультатЗапроса.Следующий() Цикл

		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
		Движение.Период = Дата;
		Движение.ВКМ_Сотрудник = РезультатЗапроса.Сотрудник;
		Движение.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ВКМ_ФиксированнаяПремия;
		СуммаУдержания = РезультатЗапроса.СуммаПремии * 13 / 100;
		Движение.ВКМ_Сумма = РезультатЗапроса.СуммаПремии - СуммаУдержания;

	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли