#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Проверяем, был ли передан параметр "АдресВХранилище"
	АдресВХранилище = "";
	Если Параметры.Свойство("АдресВХранилище", АдресВХранилище) Тогда
		ОтпускаСотрудников = ПолучитьИзВременногоХранилища(АдресВХранилище);
	Иначе
		// Если параметр не передан, создаем пустую таблицу, чтобы избежать ошибок в цикле
		ОтпускаСотрудников = Новый ТаблицаЗначений;
	КонецЕсли;
	
	// Шкала времени. Месяцы
	ШкалаМесяцы = ВКМ_ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы.Добавить();
	ШкалаМесяцы.Единица = ТипЕдиницыШкалыВремени.Месяц;
	ШкалаМесяцы.Кратность = 1;
	ШкалаМесяцы.Формат = "ДФ='ММММ гггг'";
	ШкалаМесяцы.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Сплошная, 1); 
	
	// Шкала времени. Недели
	ШкалаНедели = ВКМ_ДиаграммаГанта.ОбластьПостроения.ШкалаВремени.Элементы.Добавить();
	ШкалаНедели.Единица = ТипЕдиницыШкалыВремени.Неделя;
	ШкалаНедели.Кратность = 1;
	ШкалаНедели.Формат = "ДФ='дд.ММ'"; 
	ШкалаНедели.ЛинииДелений = Новый Линия(ТипЛинииДиаграммы.Пунктир, 1);
	
	Для Каждого Строка Из ОтпускаСотрудников Цикл  
		
		Точка = ВКМ_ДиаграммаГанта.УстановитьТочку(Строка.ВКМ_Сотрудник);
		Точка.Текст = Строка.ВКМ_Сотрудник.Наименование;
		Точка.Расшифровка = Строка.ВКМ_Сотрудник;
		
		Серия = ВКМ_ДиаграммаГанта.УстановитьСерию("Отпуск");
		Серия.Текст = "Отпуск"; 
		Серия.Цвет = WebЦвета.СинийСоСтальнымОттенком;
		
		Значение = ВКМ_ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
		Интервал = Значение.Добавить();
		Интервал.Начало = Строка.ВКМ_ДатаНачала;
		Интервал.Конец = КонецДня(Строка.ВКМ_ДатаОкончания);	
		
		СтрокаТаб = Объект.ОтпускаСотрудников.Добавить();
		СтрокаТаб.ВКМ_Сотрудник = Строка.ВКМ_Сотрудник;
		СтрокаТаб.ВКМ_ДатаНачала = Строка.ВКМ_ДатаНачала;
		СтрокаТаб.ВКМ_ДатаОкончания = Строка.ВКМ_ДатаОкончания;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти