
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт
	
	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда
		
        КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
        КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
        КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(Метаданные.Документы.РеализацияТоваровУслуг);
        КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";
		
		Возврат КомандаСоздатьНаОсновании;
		
	КонецЕсли;

	Возврат Неопределено;
	
КонецФункции

//++ ВКМ MorozovaDE: Работа с заявками клиентов
// Устанавливает признак необходимости добавления команд печати для объекта.
//
// Параметры:
//   НастройкиОбъекта - Структура - Структура настроек печати объекта.
Процедура ПриОпределенииНастроекПечати(НастройкиОбъекта) Экспорт	
	НастройкиОбъекта.ПриДобавленииКомандПечати = Истина;
КонецПроцедуры

// Добавляет команды печати для документа "Реализация товаров и услуг".
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - Таблица для хранения команд печати документа
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт об оказанных услугах
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОказанныхУслугах";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказанных услугах'");
	КомандаПечати.Порядок = 3;

КонецПроцедуры

// Формирует печатную форму "Акт об оказанных услугах" для документов "Реализация товаров и услуг".
//
// Параметры:
//   МассивОбъектов - Массив из ДокументСсылка.РеализацияТоваровУслуг - Массив ссылок на документы, для которых формируется печатная форма
//   ПараметрыПечати - Структура - Дополнительные параметры, влияющие на формирование печатной формы
//   КоллекцияПечатныхФорм - ТаблицаЗначений - Коллекция для хранения сформированных печатных форм
//   ОбъектыПечати - Соответствие из ДокументСсылка.РеализацияТоваровУслуг, Произвольный - Соответствие объектов для печати и их данных
//   ПараметрыВывода - Структура - Параметры, определяющие способ вывода печатной формы
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОбОказанныхУслугах");
	Если ПечатнаяФорма <> Неопределено Тогда
		ПечатнаяФорма.ТабличныйДокумент = ПечатьАкта(МассивОбъектов, ОбъектыПечати);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказанных услугах'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ВКМ_ПФ_MXL_АктОбОказанныхУслугах";
	КонецЕсли;

КонецПроцедуры
//-- ВКМ MorozovaDE: Работа с заявками клиентов

#КонецОбласти

//++ ВКМ MorozovaDE: Работа с заявками клиентов
#Область СлужебныеПроцедурыИФункции

Функция ПечатьАкта(МассивОбъектов, ОбъектыПечати)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Акт";
	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ВКМ_ПФ_MXL_АктОбОказанныхУслугах");
	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);
	ПервыйДокумент = Истина;

	Пока ДанныеДокументов.Следующий() Цикл

		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ПервыйДокумент = Ложь;
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		ВывестиЗаголовокАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиТелоТаблицыАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
		ВывестиПодвалАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
			ДанныеДокументов.Ссылка);

	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Дата КАК Дата,
	|	РеализацияТоваровУслуг.Номер КАК Номер,
	|	РеализацияТоваровУслуг.Ссылка КАК Ссылка,
	|	РеализацияТоваровУслуг.Организация КАК Организация,
	|	РеализацияТоваровУслуг.Контрагент КАК Контрагент,
	|	РеализацияТоваровУслуг.Договор КАК Договор,
	|	РеализацияТоваровУслуг.СуммаДокумента КАК СуммаДокумента,
	|	РеализацияТоваровУслуг.Основание КАК Основание,
	|	РеализацияТоваровУслуг.Ответственный КАК Ответственный,
	|	РеализацияТоваровУслуг.Комментарий КАК Комментарий,
	|	РеализацияТоваровУслуг.Услуги.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Количество КАК Количество,
	|		Цена КАК Цена,
	|		Сумма КАК Сумма) КАК Услуги
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Ссылка В (&МассивДокументов)";

	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);

	Возврат Запрос.Выполнить().Выбрать();

КонецФункции

Процедура ВывестиЗаголовокАкта(ДанныеДокументов, ТабличныйДокумент, Макет)

	ОбластьЗаголовокАкта = Макет.ПолучитьОбласть("Заголовок");
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");

	ДанныеПечати = Новый Структура;

	ШаблонЗаголовка = "Акт об оказанных услугах №%1 от %2";
	ТекстЗаголовка = СтрШаблон(ШаблонЗаголовка, ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
		Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);

	ДанныеПечати.Вставить("Исполнитель", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Заказчик", ДанныеДокументов.Контрагент);

	ОбластьЗаголовокАкта.Параметры.Заполнить(ДанныеПечати);

	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("Договор", ДанныеДокументов.Договор);

	ОбластьЗаголовокТаблицы.Параметры.Заполнить(ДанныеПечати);

	ТабличныйДокумент.Вывести(ОбластьЗаголовокАкта);
	ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);

КонецПроцедуры

Процедура ВывестиТелоТаблицыАкта(ДанныеДокументов, ТабличныйДокумент, Макет)

	ОбластьТело = Макет.ПолучитьОбласть("ТелоТаблицы");

	Выборка = ДанныеДокументов.Услуги.Выбрать();
	Пока Выборка.Следующий() Цикл
		ОбластьТело.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(ОбластьТело);
	КонецЦикла;

КонецПроцедуры

Процедура ВывестиПодвалАкта(ДанныеДокументов, ТабличныйДокумент, Макет)

	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");

	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("Итого", ДанныеДокументов.СуммаДокумента);

	ШаблонТекста = "Оказано услуг на общую сумму: %1 (%2)";
	ТекстСтроки = СтрШаблон(ШаблонТекста, ДанныеДокументов.СуммаДокумента, ЧислоПрописью(ДанныеДокументов.СуммаДокумента, "Л=ru_RU;ДП=Истина",
		"рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2"));
	ДанныеПечати.Вставить("ОказаноУслуг", ТекстСтроки);

	ОбластьПодвал.Параметры.Заполнить(ДанныеПечати);

	ТабличныйДокумент.Вывести(ОбластьПодвал);

КонецПроцедуры
#КонецОбласти
//-- ВКМ MorozovaDE: Работа с заявками клиентов

#КонецЕсли