
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	//++ ВКМ MorozovaDE: Работа с заявками клиентов
	// Создание команды "Заполнить" и связанной с ней кнопки на командной панели.
	КомандаЗаполнить = Команды.Добавить("ВКМ_Заполнить");
	КомандаЗаполнить.Заголовок = "Заполнить";
	КомандаЗаполнить.Действие = "ВКМ_Заполнить";

	КнопкаКоманды = Элементы.Вставить("ВКМ_Заполнить", Тип("КнопкаФормы"), КоманднаяПанель);
	КнопкаКоманды.ИмяКоманды = "ВКМ_Заполнить";
	КнопкаКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка;	
	//-- ВКМ MorozovaDE: Работа с заявками клиентов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

//++ ВКМ MorozovaDE: Работа с заявками клиентов	
&НаКлиенте
Процедура ВКМ_Заполнить(Команда)

	// Проверяет заполненность договора и вызывает процедуру заполнения на сервере.
	// Если договор не указан, выводит сообщение об ошибке.
	Если ЗначениеЗаполнено(Объект.Договор) Тогда
		ВКМ_ЗаполнитьНаСервере();
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Договор не заполнен";
		Сообщение.Сообщить();
	КонецЕсли;

КонецПроцедуры
//-- ВКМ MorozovaDE: Работа с заявками клиентов

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество;
	
	РассчитатьСуммуДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

//++ ВКМ MorozovaDE: Работа с заявками клиентов
&НаСервере
Процедура ВКМ_ЗаполнитьНаСервере()
	
	// Проверяет договор на соответствие виду "Абонентское обслуживание"
	// и валидности сроков действия. Если условия выполняются,
	// запускает процедуру автозаполнения в документе.
	Данные = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Договор,
		"ВидДоговора, ВКМ_ДатаНачала, ВКМ_ДатаОкончания");
	
	Если Данные.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
		
		Если Данные.ВКМ_ДатаНачала <= Объект.Дата И Данные.ВКМ_ДатаОкончания >= Объект.Дата Тогда
			
			ДокументОбъект = РеквизитФормыВЗначение("Объект");
			ДокументОбъект.ВКМ_ВыполнитьАвтозаполнение();
			
			ЗначениеВРеквизитФормы(ДокументОбъект, "Объект");
			
		Иначе
			ОбщегоНазначения.СообщитьПользователю("Срок действия договора истек");
		КонецЕсли;
		
	Иначе
		ОбщегоНазначения.СообщитьПользователю("Необходимо выбрать договор абонентского обслуживания");
	КонецЕсли;

КонецПроцедуры 
//-- ВКМ MorozovaDE: Работа с заявками клиентов

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти
