
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения);
	КонецЕсли;
	
КонецПроцедуры

//++ ВКМ MorozovaDE: Работа с заявками клиентов
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	//++ ВКМ MorozovaDE: Работа с заявками клиентов
	// Инициализация массива для хранения реквизитов, которые не будут проверяться
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	// Проверяем, заполнен ли договор и соответствует ли он нужному виду
	Если ЗначениеЗаполнено(Договор) Тогда
		
		// Получаем вид договора из объекта
		ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВидДоговора");
		
		// Если договор является абонентским, добавляем "Основание" в непроверяемые реквизиты
		Если ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание Тогда
			МассивНепроверяемыхРеквизитов.Добавить("Основание");
		КонецЕсли;
		
	КонецЕсли;
	
	// Удаляем непроверяемые реквизиты из общего массива проверяемых реквизитов
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	//-- ВКМ MorozovaDE: Работа с заявками клиентов

КонецПроцедуры
//-- ВКМ MorozovaDE: Работа с заявками клиентов	

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОбработкаЗаказов.Записывать = Истина;
	Движения.ОстаткиТоваров.Записывать = Истина;
	
	Движение = Движения.ОбработкаЗаказов.Добавить();
	Движение.Период = Дата;
	Движение.Контрагент = Контрагент;
	Движение.Договор = Договор;
	Движение.Заказ = Основание;
	Движение.СуммаОтгрузки = СуммаДокумента;

	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТоваров.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Контрагент = Контрагент;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Сумма = ТекСтрокаТовары.Сумма;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьНаОснованииЗаказаПокупателя(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаказПокупателя.Организация КАК Организация,
	               |	ЗаказПокупателя.Контрагент КАК Контрагент,
	               |	ЗаказПокупателя.Договор КАК Договор,
	               |	ЗаказПокупателя.СуммаДокумента КАК СуммаДокумента,
	               |	ЗаказПокупателя.Товары.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Товары,
	               |	ЗаказПокупателя.Услуги.(
	               |		Ссылка КАК Ссылка,
	               |		НомерСтроки КАК НомерСтроки,
	               |		Номенклатура КАК Номенклатура,
	               |		Количество КАК Количество,
	               |		Цена КАК Цена,
	               |		Сумма КАК Сумма
	               |	) КАК Услуги
	               |ИЗ
	               |	Документ.ЗаказПокупателя КАК ЗаказПокупателя
	               |ГДЕ
	               |	ЗаказПокупателя.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Не Выборка.Следующий() Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	
	ТоварыОснования = Выборка.Товары.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Товары.Добавить(), ТоварыОснования);
	КонецЦикла;
	
	УслугиОснования = Выборка.Услуги.Выбрать();
	Пока ТоварыОснования.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(Услуги.Добавить(), УслугиОснования);
	КонецЦикла;
	
	Основание = ДанныеЗаполнения;
	
КонецПроцедуры

//++ ВКМ MorozovaDE: Работа с заявками клиентов
// Автоматически заполняет табличную часть "Услуги" на основе данных договора и регистра накопления выполненных работ.
Процедура ВКМ_ВыполнитьАвтозаполнение() Экспорт

	// Получаем номенклатуру для абонентской платы и работ специалиста из констант.
	АбонентскаяПлата = Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить();
	РаботыСпециалиста = Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить();

	// Проверяем, что номенклатура для автозаполнения указана в константах.
	Если Не ЗначениеЗаполнено(АбонентскаяПлата) Тогда
		ОбщегоНазначения.СообщитьПользователю("Абонентская плата не заполнена");
		Возврат;
	КонецЕсли;

	Если Не ЗначениеЗаполнено(РаботыСпециалиста) Тогда
		ОбщегоНазначения.СообщитьПользователю("Работы специалиста не заполнена");
		Возврат;
	КонецЕсли;

	// Очищаем табличную часть перед заполнением.
	Услуги.Очистить();

	// Добавляем в табличную часть "Услуги" абонентскую плату, если она указана в договоре.
	АбонентскаяПлатаИзДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Договор, "ВКМ_СуммаАбонентскойПлаты");

	Если АбонентскаяПлатаИзДоговора > 0 Тогда

		НоваяСтрока = Услуги.Добавить();
		НоваяСтрока.Номенклатура = АбонентскаяПлата;
		НоваяСтрока.Количество = 1;
		НоваяСтрока.Цена = АбонентскаяПлатаИзДоговора;
		НоваяСтрока.Сумма = АбонентскаяПлатаИзДоговора;

	КонецЕсли;

	// Запрос к регистру накопления для получения данных о выполненных работах за текущий месяц.
	Запрос = Новый запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	ВКМ_ВыполненныеКлиентуРаботыОбороты.ВКМ_Договор КАК Договор,
				   |	СУММА(ЕСТЬNULL(ВКМ_ВыполненныеКлиентуРаботыОбороты.ВКМ_СуммаКОплатеОборот, 0)) КАК Сумма,
				   |	СУММА(ВКМ_ВыполненныеКлиентуРаботыОбороты.ВКМ_КоличествоЧасовОборот) КАК КоличествоЧасов
				   |ИЗ
				   |	РегистрНакопления.ВКМ_ВыполненныеКлиентуРаботы.Обороты(&ДатаНачала, &ДатаОкончания,, ВКМ_Клиент = &Клиент
				   |	И ВКМ_Договор = &Договор) КАК ВКМ_ВыполненныеКлиентуРаботыОбороты
				   |СГРУППИРОВАТЬ ПО
				   |	ВКМ_ВыполненныеКлиентуРаботыОбороты.ВКМ_Договор";

	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Дата));
	Запрос.УстановитьПараметр("Клиент", Контрагент);
	Запрос.УстановитьПараметр("Договор", Договор);

	// Обработка результата запроса и добавление в табличную часть услуг по выполненным работам.
	Результат = Запрос.Выполнить().Выбрать();
	Пока Результат.Следующий() Цикл
		Если Результат.Сумма > 0 Тогда

			НоваяСтрока = Услуги.Добавить();
			НоваяСтрока.Номенклатура = РаботыСпециалиста;
			НоваяСтрока.Количество = Результат.КоличествоЧасов;
			НоваяСтрока.Цена = Результат.Сумма;
			НоваяСтрока.Сумма = Результат.Сумма;

		КонецЕсли;
	КонецЦикла;

	// Пересчет итоговой суммы документа.
	СуммаДокумента = Товары.Итог("Сумма") + Услуги.Итог("Сумма");

КонецПроцедуры 
//-- ВКМ MorozovaDE: Работа с заявками клиентов

#КонецОбласти

#КонецЕсли
